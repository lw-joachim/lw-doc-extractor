
node_body : (description_block | description_line)? external_connections? external_variables? variables? start_sequence_lines referenced_sequences_lines*

// description
description_block.4 : "DESCRIPTION_BLOCK" _NEWLINE DESCR_LINE+ "DESCRIPTION_BLOCK_END" _NEWLINE
description_line.3 : "DESCRIPTION" DESCR_LINE

DESCR_LINE.-1 : /[^\§\n\r]*/ _NEWLINE

// external_connections
external_connections : "EXTERNAL_CONNECTIONS" _NEWLINE external_connection+ "EXTERNAL_CONNECTIONS_END" _NEWLINE
external_connection: ID _NEWLINE


// external_variables
external_variables: "EXTERNAL_VARIABLES" _NEWLINE ext_var_line+ "EXTERNAL_VARIABLES_END" _NEWLINE
ext_var_line: VARIABLE _NEWLINE

// flags
variables.2 : "VARIABLES" _NEWLINE var_line+ "VARIABLES_END" _NEWLINE
var_line : VARIABLE "=" (VALUE | str_enum) FLAG_DESCR? _NEWLINE
VALUE : ("true" | "false" | /[0-9]+/)
VARIABLE : /[A-Za-z_.0-9]+/
FLAG_DESCR : "(" /[^\)]+/ ")"
str_enum : STR_VAL "[" ENUM_VAL ("," ENUM_VAL)* "]"
STR_VAL: /[“"][A-Za-z][A-Za-z_0-9]*[”"]/
ENUM_VAL :  /[A-Za-z][A-Za-z_0-9]*/

// sequence defn
start_sequence_lines.-1 : sequence
referenced_sequences_lines.-2: "§" ID _NEWLINE sequence

sequence : _sequence_lines+
_sequence_lines.-9 : /[^\§\n\r].*/ _NEWLINE


// Terminals
ID : /[A-Za-z0-9\-_]+/

// whitespace handling

_NEWLINE: /\r?\n[\t ]*/
%import common.WS_INLINE
%ignore WS_INLINE