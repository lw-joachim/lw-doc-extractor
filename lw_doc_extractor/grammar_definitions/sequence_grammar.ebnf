
sequence : (LINE_COMMENT | inner_statement | simple_dialog_line | jump_statements | hub_statement_block | choice_dialog_statement | if_block | shac_statement_block )+
inner_sequence: (LINE_COMMENT | inner_statement | if_block | simple_dialog_line | jump_statements)+

// shac
shac_statement_block :  "-" "SHAC_CHOICE" _NEWLINE shac_choice+ "SHAC_CHOICE_END" _NEWLINE
shac_choice : CHOICE_INFINITE STATEMENT_DESCRIPTION "{" EVENT_ID "}" _NEWLINE condition? exit_instruction? inner_sequence


// hub
hub_statement_block :  "-" "HUB" _NEWLINE hub_choice+ "HUB_CHOICE_END" _NEWLINE
hub_choice : (CHOICE_SINGLE | CHOICE_INFINITE) STATEMENT_DESCRIPTION "{" EVENT_ID "}" _NEWLINE condition? exit_instruction? inner_sequence

// if
if_block : "IF" REST_OF_LINE _NEWLINE inner_sequence (if_else | if_elseif)? "END_IF" _NEWLINE
if_else : "ELSE" _NEWLINE inner_sequence
if_elseif : "ELSE_IF" REST_OF_LINE _NEWLINE inner_sequence (if_else | if_elseif)?

// jump statement
?jump_statements : internal_jump_statement | external_jump_statement

internal_jump_statement : ">" REFERENCED_ID _NEWLINE
external_jump_statement : "►" REFERENCED_ID _NEWLINE

// dialog
simple_dialog_line.-2 : ENTITY_NAME STAGE_DIRECTIONS? line_attributes? _NEWLINE SPOKEN_TEXT COMMENT? _NEWLINE

choice_dialog_statement.-1 : ENTITY_NAME _NEWLINE player_choice_block
player_choice_block : "PLAYER_CHOICE" _NEWLINE player_choice+ "PLAYER_CHOICE_END" _NEWLINE 
player_choice : (CHOICE_SINGLE | CHOICE_INFINITE) "[" MENU_SLOT? MENU_TEXT "]" SPOKEN_TEXT? STAGE_DIRECTIONS? line_attributes? _NEWLINE condition? exit_instruction? inner_sequence

line_attributes :  "[" line_attribute ("," line_attribute)* "]"

line_attribute : emotion_line_attribute

emotion_line_attribute : /emotion/ ":" LINE_ATTIBUTE_VALUE

LINE_ATTIBUTE_VALUE : /[^,\]]+/

// inner statement
?inner_statement : "-" inner_statement_cmd _NEWLINE
?inner_statement_cmd : load_scenario_statement | stage_event_statement | sync_stage_event_statement | game_event_listener_statement | mindset_notification_statement | wiki_notification_statement | set_statement | node_ref_statement | the_end_statement | start_quest_statement | end_quest_statement | activate_quest_target_statement | deactivate_quest_target_statement | comment_statement | save_game_statement

load_scenario_statement :  "LOAD_SCENARIO" STATEMENT_DESCRIPTION "{" EVENT_ID "}"
load_stage_statement : "LOAD_STAGE" STATEMENT_DESCRIPTION "{" EVENT_ID "}"
stage_event_statement : "STAGE_EVENT" STATEMENT_DESCRIPTION "{" EVENT_ID "}"
sync_stage_event_statement : "SYNC_STAGE_EVENT" STATEMENT_DESCRIPTION "{" EVENT_ID "}"
game_event_listener_statement : "GAME_EVENT_LISTENER" STATEMENT_DESCRIPTION "{" EVENT_ID "}"
comment_statement :  "COMMENT" COMMENT_TEXT
save_game_statement : "SAVE_GAME" STATEMENT_DESCRIPTION "{" SAVE_GAME_ID "}"

mindset_notification_statement : "MINDSET_NOTIFICATION" STATEMENT_DESCRIPTION "{" EVENT_ID "}"
wiki_notification_statement : "WIKI_NOTIFICATION" STATEMENT_DESCRIPTION "{" EVENT_ID "}"

set_statement : "SET" INSTRUCTION
node_ref_statement : "*" ID
the_end_statement : "THE_END" STATEMENT_DESCRIPTION?

// quest handling

start_quest_statement : "START_QUEST" "{" QUEST_ID "}" STATEMENT_DESCRIPTION _quest_attributes?
end_quest_statement : "END_QUEST" "{" QUEST_ID "}"

activate_quest_target_statement : "ACTIVATE_QUEST_TARGET" "{" QUEST_ID "}" "[" quest_targets "]"
deactivate_quest_target_statement : "DEACTIVATE_QUEST_TARGET" "{" QUEST_ID "}" "[" quest_targets "]"


_qentity : /[a-zA-Z0-9\-_:]+/
_quest_attributes: "[" [ _quest_attribute ("," _quest_attribute)* ] "]"
_quest_attribute : _targets_quest_attribute
_targets_quest_attribute : "targets" ":" quest_targets
quest_targets :  _qentity ("|" _qentity)*


//
CHOICE_SINGLE : "Θ"
CHOICE_INFINITE : "∞"

//conditions
condition : "SHOW_IF" REST_OF_LINE _NEWLINE
exit_instruction : "SET" REST_OF_LINE _NEWLINE

// Terminals
ID : /[A-Za-z0-9\-_]+/
REFERENCED_ID : /[A-Za-z0-9\-_]+/

ENTITY_NAME.-1 : /[A-Z][A-Z0-9\ ]{2,}/

SPOKEN_TEXT: /[^\n\r\(\[]+/
QUEST_ID :  /[A-Za-z0-9\-]+/

STATEMENT_DESCRIPTION : /[^\n\r\{\[]+/
EVENT_ID : /[A-Za-z0-9\-_:]+/
SAVE_GAME_ID : /[A-Za-z0-9_\-]+/

REST_OF_LINE : /[^\n\r]+/
COMMENT_TEXT : /[^\n\r]+/
INSTRUCTION : /[^\n\r]+/

MENU_SLOT : "⇐"|"⇑"|"⇒"|"⇓"|"⇖"|"⇗"|"⇘"|"⇙"
MENU_TEXT : /[^\]⇐⇑⇒⇓⇖⇗⇘⇙]+/

STAGE_DIRECTIONS : /\([^\)]*\)/

// whitespace handling

_NEWLINE: /\r?\n[\t ]*/
%import common.WS_INLINE
%ignore WS_INLINE


LINE_COMMENT.-9 : /\([^\)]*\)/ _NEWLINE
COMMENT: /\([^\)]*\)/ 
%ignore LINE_COMMENT