start : start_node node_definition+

start_node : _node_marker "Chapter" ID _node_body
  
node_definition  : _NEWLINE _node_marker node_type? ID _node_body

_node_body : node_properties? start_sequence referenced_sequence*

// node properties
node_properties:  node_description? image?
node_description : _NEWLINE DESCRIPTION_TXT
image : _NEWLINE "<" /IMAGE +[0-9]+/ ">"
DESCRIPTION_TXT : /[^\*§\n\r\-].*/


// sequence defn
start_sequence : sequence
referenced_sequence	: _NEWLINE "§" ID sequence
sequence : outer_statement+
inner_sequence: inner_statement+


// Statement stuff
?outer_statement : load_stage_statement | game_event_listener_statement | stage_event_statement | sync_stage_event_statement | hub_statement_block | choice_dialog_statement | if_block | set_statement | node_ref_statement | simple_dialog_statement | internal_jump_statement | external_jump_statement | start_quest_statement | end_quest_statement | the_end_statement | mindset_notification_statement | wiki_notification_statement
?inner_statement : set_statement | node_ref_statement | simple_dialog_statement | internal_jump_statement | external_jump_statement | game_event_listener_statement | stage_event_statement | sync_stage_event_statement | start_quest_statement | end_quest_statement | mindset_notification_statement | wiki_notification_statement

load_stage_statement :  _NEWLINE "-" "LOAD_STAGE" STATEMENT_DESCRIPTION ("{" EVENT_ID "}")?
stage_event_statement :  _NEWLINE "-" "STAGE_EVENT" STATEMENT_DESCRIPTION ("{" EVENT_ID "}")?
sync_stage_event_statement :  _NEWLINE "-" "SYNC_STAGE_EVENT" STATEMENT_DESCRIPTION ("{" EVENT_ID "}")?
game_event_listener_statement :  _NEWLINE "-" "GAME_EVENT_LISTENER" STATEMENT_DESCRIPTION ("{" EVENT_ID "}")?

mindset_notification_statement :  _NEWLINE "-" "MINDSET_NOTIFICATION" STATEMENT_DESCRIPTION ("{" EVENT_ID "}")?
wiki_notification_statement :  _NEWLINE "-" "WIKI_NOTIFICATION" STATEMENT_DESCRIPTION ("{" EVENT_ID "}")?

set_statement : _NEWLINE "-" "SET" INSTRUCTION
node_ref_statement : _NEWLINE "-" "*" ID condition? exit_instruction?
simple_dialog_statement : _NEWLINE ENTITY_NAME _NEWLINE SPOKEN_TEXT
the_end_statement :  _NEWLINE "-" "THE_END" STATEMENT_DESCRIPTION?


internal_jump_statement : _NEWLINE ">" REFERENCED_ID
external_jump_statement : _NEWLINE "►" REFERENCED_ID

start_quest_statement : _NEWLINE "-" "START_QUEST" QUEST_ID
end_quest_statement : _NEWLINE "-" "END_QUEST" QUEST_ID

choice_dialog_statement : _NEWLINE ENTITY_NAME _NEWLINE player_choice_block
player_choice_block : "PLAYER_CHOICE" player_choice+ _NEWLINE "PLAYER_CHOICE_END"
player_choice : _NEWLINE "Θ" "[" MENU_TEXT "]" SPOKEN_TEXT? condition? exit_instruction? inner_sequence


if_block : _NEWLINE "IF" REST_OF_LINE inner_sequence _NEWLINE "ELSE" inner_sequence _NEWLINE "ENDIF"

hub_statement_block :  _NEWLINE "-" "HUB" hub_choice+ _NEWLINE "HUB_CHOICE_END"
hub_choice : _NEWLINE "Θ" STATEMENT_DESCRIPTION ("{" EVENT_ID "}")? condition? exit_instruction? inner_sequence

condition : _NEWLINE "IF" REST_OF_LINE_TEXT
exit_instruction : _NEWLINE "SET" REST_OF_LINE_TEXT

STATEMENT_DESCRIPTION : /[^\n\r\{]+/
EVENT_ID : /[A-Za-z0-9\-_:]+/

MENU_TEXT : /[^\]]+/

SPOKEN_TEXT: /[^\n\r]+/
QUEST_ID : /[^\n\r]+/
REST_OF_LINE_TEXT : /[^\n\r]+/

ENTITY_NAME : /[A-Z]+/ ENTITY_COUNTER?
ENTITY_COUNTER : /\s*[0-9]+/

INSTRUCTION : /[^\n\r].*/
REST_OF_LINE : /[^\n\r].*/


// node type
node_type : "["? NODE_TYPES "]"?

NODE_TYPES : "D-DEF" | "D-EAV" | "C-CUT" | "C-SEG" | "Section"
    
_node_marker : "*"

line.9 : _NEWLINE /[^\*§\n\r].*/

COMMENT: /\([^\)]*\)/
_NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )+

ID : /[A-Za-z0-9\-_]+/
REFERENCED_ID : /[A-Za-z0-9\-_]+/

%ignore /\r?\n.../

%ignore /[\t \f]+/  // WS
%ignore COMMENT
